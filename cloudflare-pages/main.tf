resource "cloudflare_pages_project" "pages" {
  account_id        = var.account_id
  name              = var.domain_prefix
  production_branch = var.github_branch

  build_config = {
    build_command   = "npm run build"
    destination_dir = "dist"
  }

  source = {
    config = {
      production_deployments_enabled = true
      owner                          = var.github_owner
      production_branch              = var.github_branch
      repo_name                      = var.github_repo
    }
    type = "github"
  }

  deployment_configs = {
    preview = {
    }

    production = {
      env_vars = {
        "SECRET" = {
          type  = "secret_text",
          value = "VERY SECRET THING"
        },
      }
    }
  }
}

resource "cloudflare_zero_trust_access_identity_provider" "google" {
  config = {
    client_id     = var.google_client_id
    client_secret = var.google_client_secret
  }
  name       = "pages-google"
  type       = "google"
  account_id = var.account_id
}

resource "cloudflare_zero_trust_access_policy" "policy" {
  account_id = var.account_id
  decision   = "allow"
  name       = "pages-policy"

  include = [{
    email = {
      email = "${var.email}"
    }
  }]
}

resource "cloudflare_zero_trust_access_application" "access" {
  account_id       = var.account_id
  name             = "pages-access"
  domain           = cloudflare_pages_project.pages.subdomain
  session_duration = "24h"
  type             = "self_hosted"
  allowed_idps     = [cloudflare_zero_trust_access_identity_provider.google.id]
  policies = [
    {
      id         = cloudflare_zero_trust_access_policy.policy.id
      precedence = 1
    }
  ]
}

# As described here: https://github.com/cloudflare/terraform-provider-cloudflare/issues/3099
# creating a pages project won't automatically deploy it, so we need to trigger the deployment manually
resource "terraform_data" "trigger_initial_deploy" {
  triggers_replace = [
    cloudflare_pages_project.pages.id
  ]

  provisioner "local-exec" {
    command = <<EOT
      curl -X POST "https://api.cloudflare.com/client/v4/accounts/${var.account_id}/pages/projects/${cloudflare_pages_project.pages.name}/deployments" \
           -H "Authorization: Bearer $CLOUDFLARE_API_TOKEN" \
           -H "Content-Type: application/json" \
           --data '{"branch":"${var.github_branch}"}'
    EOT
    environment = {
      CLOUDFLARE_API_TOKEN = var.api_token
    }
  }
}

